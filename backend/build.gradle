plugins {
	id 'org.springframework.boot' version '2.5.0'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'org.asciidoctor.convert' version '1.5.8'
	id 'java'
	id 'info.solidsoft.pitest' version '1.5.1'
}

group = 'de.jkrech'
version = '0.0.1-SNAPSHOT'

repositories {
	mavenCentral()
}

ext {
	set('snippetsDir', file("build/generated-snippets"))
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
}

test {
	outputs.dir snippetsDir
	useJUnitPlatform()
}

asciidoctor {
	inputs.dir snippetsDir
	dependsOn test
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}

// creates build/resources/main/build-info.properties
springBoot {
	buildInfo {
		properties {
			artifact = "todo-a-rest"
			version = "${project.version}"
			group = "${project.group}"
			name = 'TODO a REST'
		}
	}
}

// liefert im Moment eine Fehlermeldung bei der Ausf√ºhrung
pitest {
	testPlugin = 'junit5'
    targetClasses = ['de.jkrech.todo.*']  //by default "${project.group}.*"
    threads = 4
    outputFormats = ['XML', 'HTML']
    timestampedReports = false
}

